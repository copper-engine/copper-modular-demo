plugins {
    id 'idea'
    id "com.github.hierynomus.license" version "0.12.1"
    id "org.beryx.jlink" version "1.2.0"
}

repositories {
    mavenCentral()
}

ext {
    copperVersion = '5.0.0-rc1'
    moduleName = 'org.copperengine.demo.jpms'
    launcherName = 'copper-modular-demo'
    imageDirPath = "$buildDir/${launcherName}-image"
    imageZipPath = "$buildDir/image-zip/${launcherName}-image.zip"
}

apply plugin: 'application'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.hierynomus.license'

sourceCompatibility = 11
targetCompatibility = 11

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

sourceSets {
    workflow {
        ext.srcDir = "$projectDir/src/workflow/java"
    }
}
sourceSets.main.java.srcDirs += sourceSets.workflow.srcDir

license {
    header rootProject.file("license-header.txt")
    skipExistingHeaders true
    ignoreFailures false
    excludes (['**/*.properties'])
}

dependencies {
    compile "org.copper-engine:copper-coreengine:$copperVersion"
    compile "org.copper-engine:copper-jmx-interface:$copperVersion"

    compile 'org.asynchttpclient:async-http-client:2.4.5'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.5'

    compile 'org.aeonbits.owner:owner:1.0.10'
    compile 'org.slf4j:slf4j-api:1.8.0-beta2'
    compile('ch.qos.logback:logback-classic:1.3.0-alpha4') {
        exclude module: "activation"
    }
    compile 'com.sun.activation:javax.activation:1.2.0'
    runtime 'org.eclipse.jdt:ecj:3.13.102'
}

mainClassName = 'org.copperengine.demo.jpms.TeamCreationMain'
jar {
    manifest {
        attributes 'Implementation-Title': "copper-demo-jpms",
                'Main-Class': mainClassName
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: classes) {
    classifier "javadoc"
    from javadoc
}

artifacts {
    archives(sourcesJar, javadocJar)
}

javadoc {
    options.addStringOption('-module-path', classpath.asPath)
}

compileJava {
    options.compilerArgs = ['--module-path', classpath.asPath]
    classpath = files()
}

tasks.withType(Javadoc) {
    options.addBooleanOption 'html5', true
}


jlink {
    launcherName = project.launcherName
    imageDir = file(imageDirPath)
    imageZip = file(imageZipPath)
    mergedModule {
        requires 'org.slf4j'
        requires 'java.compiler'
        requires 'java.naming'
        requires 'java.sql'
        requires 'java.xml'
        requires 'java.desktop'
        requires 'java.management'
        requires 'jdk.management.agent'
        requires 'jdk.unsupported'
        uses 'javax.annotation.processing.Processor'
        provides 'javax.tools.JavaCompiler' with 'org.eclipse.jdt.internal.compiler.tool.EclipseCompiler'
        provides 'com.fasterxml.jackson.core.JsonFactory' with 'com.fasterxml.jackson.core.JsonFactory'
        provides 'com.fasterxml.jackson.core.ObjectCodec' with 'com.fasterxml.jackson.databind.ObjectMapper'
    }
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    beforeZip {
        copy {
            from('src/workflow')
            into("$imageDirPath/bin/src/workflow")
        }
        copy {
            from('src/main/resources')
            into("$imageDirPath/bin")
        }
        adjustStartScripts()
    }
}

def adjustStartScripts() {
    ["$launcherName", "${launcherName}.bat"].each { script ->
        def scriptFile = new File("$imageDirPath/bin/$script")
        if(scriptFile.file) {
            def newText = scriptFile.text
            newText = newText.replace('JLINK_VM_OPTIONS=',
                    'JLINK_VM_OPTIONS=-Dio.netty.tryReflectionSetAccessible=false -Dlogback.configurationFile=./logback.xml -Djava.util.logging.config.file=logging.properties -Dfile.encoding=UTF-8 -Dcom.sun.management.jmxremote=true -Dcom.sun.management.jmxremote.authenticate=false -Dcom.sun.management.jmxremote.port=1099 -Dcom.sun.management.jmxremote.ssl=false')
            if(script.endsWith('.bat')) {
                // temporary set codepage 65001 in order to be able to use UTF-8 encoding
                newText = newText.replace('@echo off', '''
                @echo off
                setlocal ENABLEDELAYEDEXPANSION
                for /F "tokens=* USEBACKQ" %%F in (`chcp`) do (set ORIGINAL_CODEPAGE=%%F)
                set ORIGINAL_CODEPAGE=%ORIGINAL_CODEPAGE:*: =%
                set ORIGINAL_CODEPAGE=%ORIGINAL_CODEPAGE:.=%
                chcp 65001 > nul
            '''.stripIndent())
                newText += '\nchcp %ORIGINAL_CODEPAGE% > nul\n'
            }
            scriptFile.newWriter().withWriter {w -> w << newText}
        }
    }
}
